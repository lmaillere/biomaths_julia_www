{
  "hash": "e02c103b87bbfb1a192891484308b424",
  "result": {
    "markdown": "---\ntitle: \"Simulation optimisée\"\n---\n\n## Modèle de Rosenzweig MacArthur\n\nNous considérons le modèle de dynamique de populations de Rosenzweig et MacArthur que nous avons déjà vu (@Rosenzweig1963, @Turchin2003, @Smith2008).\n\n$$\n\\left\\{\\begin{array}{l}\n\\dot x = \\displaystyle rx\\left(1-\\frac{x}{K}\\right) - c \\frac{x}{h+x} y\\\\[.3cm]\n\\dot y = b\\displaystyle \\frac{x}{h+x} y - m y\n\\end{array}\\right.\n$$ {#eq-rma}\n\nL'objectif est de réaliser des simulations performantes, et le tracé du diagramme de bifurcation avec l'estimation par simulation du cycle limite. Ce type de simulations lourdes se prête bien à se genre de benchmark.\n\n### Stratégie pour le code\n\nPour un tel problème de dimension réduite, nous allons utiliser des static arrays (tableaux à adresse fixe dans la mémoire^[depuis le package `StaticArrays.jl`]), ce qui permettra de ne pas crééer une multitude d'objets pour la simulation mais de toujours modifier le même objet en mémoire.\n\nPar ailleurs nous allons essayer de nous conformer au maximum aux préconisations :\n\n- ne pas utiliser de variables globales\n- définir des fonctions\n- mettre les paramètres dans un nombre limité de variables et les passer en arguments des fonctions\n\nPour ce dernier point, nous allons définir des types (`struct`) spécifiques.\n\nNous commençons par importer les packages que nous allons utiliser:\n\n::: {.cell execution_count=1}\n``` {.julia .cell-code}\nusing StaticArrays\nusing DifferentialEquations\nusing CairoMakie\n```\n:::\n\n\n### Type spécifique pour les paramètres\n\nNous définissons un `struct` pour les paramètres du modèle de Rosenzweig MacArthur.\n\nType pour les paramètres :\n\n::: {.cell execution_count=2}\n``` {.julia .cell-code}\n@kwdef struct ParRma\n    r::Number = 1.0\n    K::Number = 10.0\n    c::Number = 1.0\n    h::Number = 2.0\n    b::Number = 2.0\n    m::Number = 1.0\nend\n```\n:::\n\n\n::: {.callout-note}\nLa macro `@kwdef` permet de renseigner des valeurs par défaut des champs du `struct`.\n:::\n\nOn peut créer des objets de type `ParRma` via les `constructor` ; on accède à un champ particulier via `objet.champ` :\n\n::: {.cell execution_count=3}\n``` {.julia .cell-code}\np_rma = ParRma() # constructor with default values\np_rma2 = ParRma(K = 8.0) # default values except K = 20.0\n\n@show p_rma\n@show p_rma.K\n@show p_rma2.K; # semi colon do not show output of par_rma2.K\n```\n\n::: {.cell-output .cell-output-stdout}\n```\np_rma = ParRma(1.0, 10.0, 1.0, 2.0, 2.0, 1.0)\np_rma.K = 10.0\np_rma2.K = 8.0\n```\n:::\n:::\n\n\n::: {.callout-note}\nLa macro `@show` est assez explicite.\n:::\n\n### Fonctions\n\nNous définissons les différentes fonctions impliquées dans le modèle de Rosenzweig MacArthur, la logistique et la réponse fonctionnelle de Holling II.\n\nPour la logistique :\n\n::: {.cell execution_count=4}\n``` {.julia .cell-code}\nfunction logistic(x::Number, p::ParRma)\n    (; r, K) = p    # deconstruct/get r and K from p\n    return r*x*(1-x/K)\nend\n```\n:::\n\n\n::: {.callout-note}\n- les notations `var::Type` permettent de spécifier le type de l'argument\n- la notation `(; r, K) = p` permet d'extraire (*deconstruct*) les champs `r` et `K` du paramètre `p`\n:::\n\nPour la réponse fonctionnelle (sans le paramètre $c$) :\n\n::: {.cell execution_count=5}\n``` {.julia .cell-code}\nfunction holling2(x::Number, p::ParRma)\n    (; h) = p   # deconstruct h from p\n    return x/(x+h)\nend\n```\n:::\n\n\n### Conditions initiales\n\nPour utiliser les static arrays avec `DifferentialEquations.jl` il faut que la condition initiale (donc l'état) et les dérivées soient des static arrays (ici un `SVector`)\n\n::: {.cell execution_count=6}\n``` {.julia .cell-code}\nconst x0 = 1.0\nconst y0 = 1.95\n\n# SVector are immutables\nu0 = SVector(x0, y0)\n```\n\n::: {.cell-output .cell-output-display execution_count=112}\n```\n2-element SVector{2, Float64} with indices SOneTo(2):\n 1.0\n 1.95\n```\n:::\n:::\n\n\n### Modèle\n\nOn définit les équations du modèle en exploitant les fonctions définies plus haut et la structure des paramètres, en renvoyant les dérivées sous forme de `SVector` :\n\n::: {.cell execution_count=7}\n``` {.julia .cell-code}\nfunction mod_rma(u::SVector{2}, p::ParRma, t)\n    (; c, b, m) = p     # get c, b, m from p\n    x = u[1]            # use x, y notations\n    y = u[2]\n\n    dx = logistic(x, p) - c * holling2(x,p) * y\n    dy = b * holling2(x, p) * y - m * y\n\n    return SVector(dx, dy) # return derivatives as SVector\nend\n```\n:::\n\n\n### Simulation simple\n\nOn définit les paramètres du temps dans un `struct` :\n\n::: {.cell execution_count=8}\n``` {.julia .cell-code}\n@kwdef struct ParTime\n    tspan::Tuple{Number, Number} = (0.0, 60.0)\n    tstep::Number = 0.1\nend\n\n# construct a time parameter\np_time = ParTime()\n```\n:::\n\n\nOn définit une fonction qui définit le problème de simulation, l'intègre et retourne la solution, avec pour arguments positionnels la condition initiale, les paramètres et les paramètres de temps, et comme keyword argument le paramètre booléen `final`.\n\nLorsque `final=false` (par défaut), la fonction renvoie toute la solution. Lorsque `final=true` la fonction renvoie la valeur finale de la simulation, ce sont nous nous servirons plus bas dans l'estimation des extremas du cycle limite.\n\n::: {.cell execution_count=9}\n``` {.julia .cell-code}\nfunction sim_rma(u0::SVector{2}, p::ParRma, pt::ParTime; final::Bool = false)\n    # deconstruct time parameter\n    (; tspan, tstep) = pt\n\n    # define and solve simulation problem\n    prob_rma = ODEProblem(mod_rma, u0, tspan, p)\n    if !final   # if final is false get whole solution\n        sol_rma = solve(prob_rma; reltol = 1e-6, saveat = tstep)\n    else        # if final is true get only final solution\n        sol_rma = solve(\n            prob_rma;\n            reltol = 1e-6,\n            save_everystep = false,\n            save_start = false,\n        )\n    end\n\n    return sol_rma\nend\n\nsim_rma(u0, p_rma, p_time)\n```\n:::\n\n\nEt on définit une fonction qui simule et produit un graphique de la simulation contre le temps, avec pour arguments la condition initiale, les paramètres et les paramètres de temps :\n\n::: {.cell execution_count=10}\n``` {.julia .cell-code}\nfunction plot_rma(u0::SVector{2}, p::ParRma, pt::ParTime)\n    # compute the Simulation\n    sol_rma = sim_rma(u0, p, pt)\n\n    # initialize figure\n    fig = Figure(; fontsize = 20)\n    ax = Axis(fig[1,1];\n        title = \"Modèle de Rosenzweig MacArthur\\n \",\n        xlabel = \"temps\",\n        ylabel = \"densités\",\n    )\n\n    # plot solution\n    lines!(ax, sol_rma.t, sol_rma[1,:]; lw = 2, label = \"proies\")\n    lines!(ax, sol_rma.t, sol_rma[2,:]; lw = 2, label = \"prédateurs\")\n    axislegend(; position = :lt)\n\n    return fig\nend\n```\n:::\n\n\nFinalement on exécute cette fonction pour tracer la simulation :\n\n::: {.cell execution_count=11}\n``` {.julia .cell-code}\n@time plot_rma(u0, p_rma, p_time)\n```\n\n::: {.cell-output .cell-output-stdout}\n```\n  0.030520 seconds (274.65 k allocations: 12.755 MiB, 33.39% compilation time)\n```\n:::\n\n::: {.cell-output .cell-output-display execution_count=117}\n![Simulation des trajectoires du modèle de Rosenzweig MacArthur](annexe_swift_rma_files/figure-html/fig-rma-time-output-2.svg){#fig-rma-time}\n:::\n:::\n\n\n::: {.callout-note}\nLa macro `@time` permet de calculer le temps mis pour calculer la commande qui la suit, ici la simulation et la création du graphique.\n:::\n\n### Diagramme de bifurcations\n\n\n\n## Cas des modèles de plus grandes dimensions\n\nPour les modèles de plus grandes dimensions ($n>8$), l'avantage en performance des static arrays n'est plus si net et la documentation de `DifferentialEquations.jl` recommande d'utiliser la version en place (is in place, IIP dans le jargon du package) de l'interface problem/solver du package.\n\nIl s'agit ici de définir le modèle non pas comme renvoyant la dérivée en fonction de l'état, des paramètres et du temps, mais comme une fonction d'arguments la dérivée, l'état, les paramètres et le temps qui modifie en place la dérivée (et ne renvoie rien)). Celà permet de muter un même objet dérivée `du` à chaque fois que le modèle est appelé, plutôt que de créer un nouvel objet dérivée `du` à chaque appel du modèle (c'est aussi ce qui est fait, différemment, avec les static arrays plus haut).\n\nTypiquement ce type de modèle IIP (en place) s'écrit:\n\n::: {.cell execution_count=12}\n``` {.julia .cell-code}\nfunction mod_rma!(du, u, p, t)\n    return nothing\nend\n```\n:::\n\n\nLa définition du problème d'intégration et l'appel de solve est similaire aux autres méthodes, à ceci près que la condition initiale et la dérivée doit être mutable, ce qui ne permet pas (ou très difficilement) d'utiliser la méthode en dimension 1. En effet une déclaration `u0 = 1.0` ou `du = 3.0` n'est pas mutable^[alors que `u0 = [1.0, 2.0]` ou `du =[2.0, 3.0]` le sont. Plus sur la mutabilité dans les [Julia notes](https://m3g.github.io/JuliaNotes.jl/stable/immutable/).].\n\n",
    "supporting": [
      "annexe_swift_rma_files"
    ],
    "filters": [],
    "includes": {}
  }
}